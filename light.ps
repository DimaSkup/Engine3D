//////////////////////////////////
// Filename: light.ps
// Revising: 18.04.22
//////////////////////////////////

//////////////////////////////////
// GLOBALS
//////////////////////////////////
Texture2D shaderTexture;
SamplerState SampleType;

cbuffer LightBuffer
{
	float4 ambientColor;
	float4 diffuseColor;
	float3 lightDirection;
	float  padding;
};

//////////////////////////////////
// TYPEDEFS
//////////////////////////////////
struct PixelInputType
{
	float4 position : SV_POSITION;
	float2 tex      : TEXCOORD0;
	float3 normal   : NORMAL;
};

//////////////////////////////////
// Pixel Shader
//////////////////////////////////
float4 LightPixelShader(PixelInputType input): SV_TARGET
{
	float4 textureColor;
	float3 lightDir;
	float  lightIntensity;
	float4 color;

	// sample the pixel color from the texture using the sampler at this texture coordinate location
	textureColor = shaderTexture.Sample(SampleType, input.tex);

	// Set the default output colour to the ambient light value for all pixels
	color = ambientColor;

	// invert the light direction for calculations
	lightDir = -lightDirection;

	// calculate the amount of light on this pixel
	lightIntensity = saturate(dot(input.normal, lightDir));

	// it the N dot L is greater that zero we add the diffuse color to the ambient color
	if (lightIntensity > 0.0f)
	{
		// determine the final diffuse color based of the diffuse color and the amount of light intensity
		color += (diffuseColor * lightIntensity);
	}

	// saturate the final light color
	color = saturate(color);

	// multiply the texture pixel and the final diffuse color to get the final pixel color result
	color = color * textureColor;

	return color;
}